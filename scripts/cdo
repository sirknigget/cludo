#!/usr/bin/env zsh
# cdo — Claude Haiku terminal command assistant
# Usage: cdo <describe what you want to do>

if [[ $# -eq 0 ]]; then
  printf "cdo> "
  read -r user_prompt
  if [[ -z "$user_prompt" ]]; then
    echo "Usage: cdo <describe what you want to do>" >&2
    echo "       cdo          (interactive — avoids shell issues with special characters)" >&2
    exit 1
  fi
else
  user_prompt="$*"
fi
system_prompt='You are a shell command assistant for macOS/zsh.
The user will describe what they want to do in the terminal in their own environment.
If the request is clear, populate "command" with the shell command they should run (EXACTLY one line).
If the request is ambiguous or unclear, populate "clarification" with a short question asking what you need to know (EXACTLY one line).
Only one field should be non-empty. No multiline answers, no lists, no explanations.
Unless specified otherwise, the user refers to the current directory as the context for commands.'

json_schema='{"type":"object","properties":{"command":{"type":"string"},"clarification":{"type":"string"}},"additionalProperties":false}'

response=$(claude --model claude-haiku-4-5-20251001 --system-prompt "$system_prompt" --json-schema "$json_schema" --output-format json --tools "" -p "User request: ${user_prompt}" 2>/dev/null)

if [[ -z "$response" ]]; then
  echo "cdo: no response returned" >&2
  exit 1
fi

if [[ -n "$DEBUG" ]]; then
  echo ""
  printf "%s" "$response"
  echo ""
fi

eval "$(printf '%s' "$response" | python3 -c '
import sys, json, shlex
try:
    data = json.load(sys.stdin).get("structured_output", {})

    # Get values, default to empty string, and strip surrounding whitespace/newlines
    cmd = str(data.get("command") or "").strip()
    clarification= str(data.get("clarification") or "").strip()

    print(f"cmd={shlex.quote(cmd)}")
    print(f"clarification={shlex.quote(clarification)}")
except Exception:
    pass
')"

if [[ -n "$clarification" ]]; then
  echo ""
  printf "  \033[1;33m%s\033[0m\n" "$clarification"
  echo ""
  exit 0
fi

if [[ -z "$cmd" ]]; then
  echo "cdo: could not parse command from response" >&2
  exit 1
fi

echo ""
printf "  \033[1;36m%s\033[0m\n" "$cmd"
echo ""
printf "Run? [y]es / [n]o / [c]opy to clipboard: "
read -r choice
echo ""

case "$choice" in
  y|Y)
    eval "$cmd"
    ;;
  c|C)
    printf '%s' "$cmd" | pbcopy
    echo "Copied to clipboard."
    ;;
  *)
    echo "Aborted."
    ;;
esac
